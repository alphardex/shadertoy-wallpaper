<style>
    body {
        margin: 0
    }
</style>
<shader-toy>
    <script type="frag">
        // https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83
        float hash(float n){return fract(sin(n)*1e4);}
        float hash(vec2 p){return fract(1e4*sin(17.*p.x+p.y*.1)*(.1+abs(sin(p.y*13.+p.x))));}
        
        float vnoise(float x){
            float i=floor(x);
            float f=fract(x);
            float u=f*f*(3.-2.*f);
            return mix(hash(i),hash(i+1.),u);
        }
        
        float vnoise(vec2 x){
            vec2 i=floor(x);
            vec2 f=fract(x);
            
            // Four corners in 2D of a tile
            float a=hash(i);
            float b=hash(i+vec2(1.,0.));
            float c=hash(i+vec2(0.,1.));
            float d=hash(i+vec2(1.,1.));
            
            // Simple 2D lerp using smoothstep envelope between the values.
            // return vec3(mix(mix(a, b, smoothstep(0.0, 1.0, f.x)),
            //			mix(c, d, smoothstep(0.0, 1.0, f.x)),
            //			smoothstep(0.0, 1.0, f.y)));
            
            // Same code, with the clamps in smoothstep and common subexpressions
            // optimized away.
            vec2 u=f*f*(3.-2.*f);
            return mix(a,b,u.x)+(c-a)*u.y*(1.-u.x)+(d-b)*u.x*u.y;
        }
        
        // This one has non-ideal tiling properties that I'm still tuning
        float vnoise(vec3 x){
            const vec3 step=vec3(110,241,171);
            
            vec3 i=floor(x);
            vec3 f=fract(x);
            
            // For performance, compute the base input to a 1D hash from the integer part of the argument and the
            // incremental change to the 1D based on the 3D -> 1D wrapping
            float n=dot(i,step);
            
            vec3 u=f*f*(3.-2.*f);
            return mix(mix(mix(hash(n+dot(step,vec3(0,0,0))),hash(n+dot(step,vec3(1,0,0))),u.x),
            mix(hash(n+dot(step,vec3(0,1,0))),hash(n+dot(step,vec3(1,1,0))),u.x),u.y),
            mix(mix(hash(n+dot(step,vec3(0,0,1))),hash(n+dot(step,vec3(1,0,1))),u.x),
            mix(hash(n+dot(step,vec3(0,1,1))),hash(n+dot(step,vec3(1,1,1))),u.x),u.y),u.z);
        }
        
        vec3 cosPalette(float t,vec3 a,vec3 b,vec3 c,vec3 d){
            return a+b*cos(6.28318*(c*t+d));
        }
        
        #define OCTAVES 8
        
        float fbm(in vec2 p){
            float sum=0.;
            float amp=.5;
            float scale=1.;
            for(int i=0;i<OCTAVES;i++){
                sum+=vnoise(p*scale)*amp;
                amp*=.6;
                scale*=1.9;
            }
            return sum;
        }
        
        float wave(in vec2 p){
            float displacement=iTime;
            vec2 aOffset=vec2(sin(displacement*.02),sin(displacement*.1))*8.;
            float a=fbm(p*3.+aOffset);
            vec2 bOffset=vec2(sin(displacement*.1),sin(displacement*.1))*2.;
            float b=fbm((p+a)*.5+bOffset);
            vec2 cOffset=vec2(sin(displacement*-.01),sin(displacement*.1))*4.;
            float c=fbm((p+b)*2.+cOffset);
            return c;
        }
        
        void mainImage(out vec4 fragColor,in vec2 fragCoord)
        {
            // Normalized pixel coordinates (from 0 to 1)
            vec2 uv=fragCoord/iResolution.xy;
            uv-=.5;
            uv.x*=iResolution.x/iResolution.y;
            
            float noise=pow(wave(uv),2.);
            vec3 col=cosPalette(noise,vec3(128.,128.,128.)/255.,vec3(123.,73.,35.)/255.,vec3(255.,255.,255.)/255.,vec3(0.,25.,50.)/255.);
            
            // Output to screen
            fragColor=vec4(col,1.);
        }
    </script>
</shader-toy>
<script src="https://unpkg.com/kokomi.js@1.9.37/build/kokomi.umd.js"></script>
<script>
    kokomi.ShaderToyElement.register();
</script>